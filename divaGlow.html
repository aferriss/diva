
<html>
<head>
	<title>Going Deep</title>
	<link rel="stylesheet" type="text/css" href="diva.css">

</head>
<body>
	<div id="container">
		<canvas id="canvas"></canvas>
	</div>

	
	<script id="vertexShader" type="x-shader/x-vertex">
		varying vec2 v_texCoord;

		void main() {

			v_texCoord = uv;
		
			gl_Position =  projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
		}
	</script>

	<script id="vertZoom" type="x-shader/x-vertex">
			attribute vec3 pos;

			varying vec2 v_texCoord;

			vec4 zoom = vec4(1.02,1.02,1.0,1.0);

			void main() {
				v_texCoord = uv;
				gl_Position =  projectionMatrix * modelViewMatrix* (vec4( position, 1.0 )*zoom);

			}
	</script>

	<script id="dispVertexShader" type="x-shader/x-vertex">
		varying vec2 v_texCoord;
		//varying vec3 vecPos;
		//varying vec3 vecNormal;
		vec3 newPos;
		uniform sampler2D u_image;
		vec4 dv;
		float dispAmount;
		void main() {

			v_texCoord = uv;
			dv = texture2D(u_image, v_texCoord);
			dispAmount = (dv.r + dv.g + dv.b)/3.0;

			newPos = vec3(normal * dispAmount * 300.0) + position;
			//vecPos = (modelMatrix * vec4(position, 1.0)).xyz;
			//vecNormal = (modelMatrix * vec4(normal, 0.0)).xyz;
			gl_Position =  projectionMatrix * modelViewMatrix * vec4( newPos, 1.0 );
		}
	</script>
	<script id="baseShader" type="x-shader/x-fragment">
		varying vec2 v_texCoord;
		uniform sampler2D u_image;

		vec3 rgb2hsv(vec3 c){
		    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
		    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
		    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
		    
		    float d = q.x - min(q.w, q.y);
		    float e = 1.0e-10;
		    return vec3(abs(( (q.z + (q.w - q.y) / (6.0 * d + e))) ), d / (q.x + e), q.x);
		}

		void main(){
			vec4 fc = texture2D(u_image, v_texCoord);
			vec3 hsv = rgb2hsv(fc.rgb);

			float avg = dot(fc.rgb, vec3(1.0))/3.0;
			//fc = avg > 0.4 ? vec4(0.0) : fc;
			if(hsv.g < 0.15 && hsv.b > 0.1 ){
			//	discard;
			}
			gl_FragColor = vec4(fc.rgb, 1.0);
		}

	</script>


	<script id="fbEq" type="x-shader/x-fragment">
		varying vec2 v_texCoord;
		uniform sampler2D u_image;

		void main(){
			vec4 t = texture2D(u_image, v_texCoord);
			//float avg = dot(vec3(1.0), t.rgb)/3.0;
			//t = avg < 0.5 ? vec4(0.0) : t;
			gl_FragColor = t;
		}
	</script>

	<script id="diffFs" type="x-shader/x-fragment">
		
		uniform sampler2D prevFrame;
		uniform sampler2D videoTexture;
		uniform sampler2D fbTex;

		varying vec2 v_texCoord;

		void main(){
			
			vec4 t1 = texture2D(prevFrame, v_texCoord);
			vec4 t2 = texture2D(videoTexture, v_texCoord);
			vec4 fb = texture2D(fbTex, v_texCoord);

			vec4 fc = (t1 - t2)*2.0 + fb;
			//float avg = dot(fc.rgb, vec3(1.0))/3.0;
			gl_FragColor = vec4(fc.rgb, 1.0);
		}
	</script>

	<script id="fbShader" type="x-shader/x-fragment">
		varying vec2 v_texCoord;
		uniform sampler2D tex;

		vec3 rgb2hsv(vec3 c){
		    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
		    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
		    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
		    
		    float d = q.x - min(q.w, q.y);
		    float e = 1.0e-10;
		    return vec3(abs(( (q.z + (q.w - q.y) / (6.0 * d + e))) ), d / (q.x + e), q.x);
		}

		vec3 hsv2rgb(vec3 c){
	    	vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	    	vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
	    	return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
		}

		void main(){
			vec4 fc = texture2D(tex, v_texCoord);
			vec3 hsv = rgb2hsv(fc.rgb);

			hsv.r += 0.001;
			hsv.r = mod(hsv.r, 1.0);
			hsv.g += 0.005;
			hsv.b += 0.005;

			vec3 rgb = hsv2rgb(hsv);
			float avg = dot(rgb, vec3(1.0))/3.0;
			if(avg < 0.1){
			//	discard;
			}
			gl_FragColor = vec4(rgb, 1.0);
		}

	</script>

	<script id="glowFrag" type="x-shader/x-fragment">
	uniform sampler2D srcTex;
	uniform vec2 step;
	uniform float time;

	varying vec2 v_texCoord;

	vec2 offset[9];

	float kernel[9];
	float kernel2[9];

	float x = 0.0;
	float y = 0.0;

	vec4 color = vec4(0.0);
	vec4 sumX = vec4(0.0);
	vec4 sumY = vec4(0.0);

	void main(){

		vec2 uv = v_texCoord;
		
		//sample grid around pixel
		offset[0] = vec2(-step.x, -step.y); // top left
		offset[1] = vec2(0.0, -step.y); // top middle
		offset[2] = vec2(step.x, -step.y); // top right
		offset[3] = vec2(-step.x, 0.0); // middle left
		offset[4] = vec2(0.0, 0.0); //middle
		offset[5] = vec2(step.x, 0.0); //middle right
		offset[6] = vec2(-step.x, step.y); //bottom left
		offset[7] = vec2(0.0, step.y); //bottom middle
		offset[8] = vec2(step.x, step.y); //bottom right

		//horizontal offset
		kernel[0] = -1.0; kernel[1] = 0.0; kernel[2] = 1.0;
		kernel[3] = -2.0; kernel[4] = 0.0; kernel[5] = 2.0;
		kernel[6] = -1.0; kernel[7] = 0.0; kernel[8] = 1.0;

		//vertical offset
		kernel2[0] = -1.0; kernel2[1] = -2.0; kernel2[2] = -1.0;
		kernel2[3] = 0.0; kernel2[4] = 0.0; kernel2[5] = 0.0;
		kernel2[6] = 1.0; kernel2[7] = 2.0; kernel2[8] = 1.0;

		//calculate sobel
		for( int i = 0; i<9; i++){
			color = texture2D(srcTex, uv+offset[i]);
			sumX += color * kernel[i];

			color = texture2D(srcTex, uv+offset[i]);
			sumY += color * kernel2[i];
		}

		// convert textures to luma
		x = 0.2126*sumX.r + 0.7152*sumX.g + 0.0722*sumX.b;
		y = 0.2126*sumY.r + 0.7152*sumY.g + 0.0722*sumY.b;

		//square luma values to denoise
		float g = x*x + y*y;

		// add sobel result to srcTex
		vec4 glowTex = texture2D(srcTex, uv);
		
		// sine wave on the blue channel
		glowTex += vec4(g+0.25, (g*0.5)*(sin(time*2.0)/2.0 + 0.3), g*(sin(time*2.0)/2.0 + 0.5), 1.0);

		gl_FragColor = glowTex;
	}
	</script>

	<script id="blurH" type="x-shader/x-fragment">
		uniform sampler2D srcTex;
		uniform vec2 step;

		varying vec2 v_texCoord;

		// gaussian blur filter modified from Filip S. at intel 
		// https://software.intel.com/en-us/blogs/2014/07/15/an-investigation-of-fast-real-time-gpu-based-image-blur-algorithms
		vec3 GaussianBlur( sampler2D tex0, vec2 centreUV, vec2 pixelOffset ){                                                                                                                                                                    
			vec3 colOut = vec3( 0.0, 0.0, 0.0 );                                                                                                                                   
			                                                                                                                                                                  
			const int stepCount = 9;
			float gWeights[stepCount];
			    gWeights[0] = 0.10855;
			    gWeights[1] = 0.13135;
			    gWeights[2] = 0.10406;
			    gWeights[3] = 0.07216;
			    gWeights[4] = 0.04380;
			    gWeights[5] = 0.02328;
			    gWeights[6] = 0.01083;
			    gWeights[7] = 0.00441;
			    gWeights[8] = 0.00157;

			float gOffsets[stepCount];
			    gOffsets[0] = 0.66293;
			    gOffsets[1] = 2.47904;
			    gOffsets[2] = 4.46232;
			    gOffsets[3] = 6.44568;
			    gOffsets[4] = 8.42917;
			    gOffsets[5] = 10.41281;
			    gOffsets[6] = 12.39664;
			    gOffsets[7] = 14.38070;
			    gOffsets[8] = 16.36501;
			

			for( int i = 0; i < stepCount; i++ ){                                                                                                                                                                
			    vec2 texCoordOffset = gOffsets[i] * pixelOffset;                                                                                                           
			    vec3 col = texture2D( tex0, centreUV + texCoordOffset ).xyz + texture2D( tex0, centreUV - texCoordOffset ).xyz;                                                
			    colOut += gWeights[i] * col;                                                                                                                               
			}

			return colOut;                                                                                                                                                   
		} 


		void main(){

			vec2 uv = v_texCoord;
			vec3 blurredTex = GaussianBlur(srcTex, vec2(1.0-uv.x, uv.y), vec2(step.x*1.5, 0.0 ) );

			gl_FragColor = vec4(blurredTex, 1.0);
		}
	</script>
	<script id="blurV" type="x-shader/x-fragment">
		uniform sampler2D srcTex;
		uniform vec2 step;

		varying vec2 v_texCoord;

		// gaussian blur filter modified from Filip S. at intel 
		// https://software.intel.com/en-us/blogs/2014/07/15/an-investigation-of-fast-real-time-gpu-based-image-blur-algorithms
		vec3 GaussianBlur( sampler2D tex0, vec2 centreUV, vec2 pixelOffset ){                                                                                                                                                                    
			vec3 colOut = vec3( 0.0, 0.0, 0.0 );                                                                                                                                   
			                                                                                                                                                                  
			const int stepCount = 9;
			float gWeights[stepCount];
			    gWeights[0] = 0.10855;
			    gWeights[1] = 0.13135;
			    gWeights[2] = 0.10406;
			    gWeights[3] = 0.07216;
			    gWeights[4] = 0.04380;
			    gWeights[5] = 0.02328;
			    gWeights[6] = 0.01083;
			    gWeights[7] = 0.00441;
			    gWeights[8] = 0.00157;

			float gOffsets[stepCount];
			    gOffsets[0] = 0.66293;
			    gOffsets[1] = 2.47904;
			    gOffsets[2] = 4.46232;
			    gOffsets[3] = 6.44568;
			    gOffsets[4] = 8.42917;
			    gOffsets[5] = 10.41281;
			    gOffsets[6] = 12.39664;
			    gOffsets[7] = 14.38070;
			    gOffsets[8] = 16.36501;
			

			for( int i = 0; i < stepCount; i++ ){                                                                                                                                                                
			    vec2 texCoordOffset = gOffsets[i] * pixelOffset;                                                                                                           
			    vec3 col = texture2D( tex0, centreUV + texCoordOffset ).xyz + texture2D( tex0, centreUV - texCoordOffset ).xyz;                                                
			    colOut += gWeights[i] * col;                                                                                                                               
			}

			return colOut;                                                                                                                                                   
		} 


		void main(){
			
			vec2 uv = v_texCoord;
			vec3 blurredTex = GaussianBlur(srcTex, vec2(uv.x, 1.0-uv.y), vec2(0.0, step.y*1.5 ) );

			gl_FragColor = vec4(blurredTex, 1.0);
		}

	</script>
	<script id="bloom" type="x-shader/x-fragment">
		uniform sampler2D srcTex;
		uniform sampler2D blurTex;

		varying vec2 v_texCoord;


		void main(){

			vec2 uv = v_texCoord;

			vec3 camColor = texture2D(srcTex, uv).rgb;
			vec3 blurColor = texture2D(blurTex, uv).rgb;

			float avg = dot(blurColor, vec3(1.0))/3.0;

			//mix the cam and the blur according to the blur's avg brightness
			//not sure if this is the right way to do this
			vec3 bloom = mix(camColor, blurColor, clamp(avg*1.5, 0.0, 1.0));

			gl_FragColor = vec4(bloom, 1.0);
		}
	</script>

	<script id="kaliShader" type="x-shader/x-fragment">
	precision highp float;
	varying vec2 v_texCoord;
	uniform sampler2D tex;
	uniform float time;

	float luma(vec3 color) {
			return dot(color.rgb, vec3(0.299, 0.587, 0.114));
	}
	
	vec3 Kali( vec3 v , vec3 s ){
	    for( int i = 0; i < 3 ; i ++){   
	        v=abs(v)/dot(v,v)+s;
	    }
	    return v;
	}

	float colormap_red(float x) {
		if (x < 0.6193682068820651) {
			return ((1.41021531432983E+02 * x - 3.78122271460656E+02) * x - 1.08403692154170E+02) * x + 2.45743977533647E+02;
		} else {
			return ((-8.63146749682724E+02 * x + 1.76195389457266E+03) * x - 1.43807716183136E+03) * x + 4.86922446232568E+02;
		}
	}

	float colormap_green(float x) {
		return (-1.37013460576160E+02 * x - 4.54698187198101E+01) * x + 2.52098684286706E+02;
	}

	float colormap_blue(float x) {
		if (x < 0.5062477983469252) {
			return ((3.95067226937040E+02 * x - 4.52381961582927E+02) * x - 1.25304923569201E+02) * x + 2.43770002412197E+02;
		} else {
			return ((2.98249378459208E+02 * x - 6.14859580726999E+02) * x + 2.22299590241459E+02) * x + 1.21998454489668E+02;
		}
	}

	vec4 colormap(float x) {
		float r = clamp(colormap_red(x) / 255.0, 0.0, 1.0);
		float g = clamp(colormap_green(x) / 255.0, 0.0, 1.0);
		float b = clamp(colormap_blue(x) / 255.0, 0.0, 1.0);
		return vec4(r, g, b, 1.0);
	}



	
	void main(){
		vec3 colA = texture2D(tex, v_texCoord).xyz;
		vec2 uv = v_texCoord;
		float avg = dot(vec3(1.0), colA) / 3.0;
		float l = luma(colA);
		//vec3 kali3 = Kali( vec3( -uv.x, -uv.y, (mod(time*0.025,1.0) )), vec3(-abs(colA.rgb)));
		vec3 kali3 = Kali( vec3( 0.5-uv.x, 0.5-uv.y, (mod(time*0.025,1.0) )), vec3(-abs(l)));
		//kali3 =  l >0.5 ? (  kali3 ) : 1.0- (kali3);
		float ll = clamp(luma(kali3), 0.0,1.0);
		vec4 fc = colormap(ll);
		gl_FragColor =vec4(kali3, 1.0);
	}

	</script>

		<script type="x-shader/x-vertex" id="semVert">
	attribute vec4 tangent;

	uniform vec2 repeat;
	uniform float useNormal;
	uniform float useRim;

	varying vec2 vUv;
	varying vec3 vTangent;
	varying vec3 vBinormal;
	varying vec3 vNormal;
	varying vec3 vEye;
	varying vec3 vU;
	varying vec2 vN;

	void main() {
		vec4 p = vec4(position, 1.0);
		vU = normalize( vec3( modelViewMatrix * p ) );

		if( useNormal == 0. ) {
			vec3 n = normalize( normalMatrix * normal );

			vec3 r = reflect( vU, n );
			float m = 2. * sqrt( 
        		pow( r.x, 2. ) + 
       			pow( r.y, 2. ) + 
        		pow( r.z + 1., 2. ) 
    		);
    		vN = r.xy / m + .5;
		} else {
			vN = vec2( 0. );
		}

		vUv = repeat * uv;
		

		vNormal = normalize( normalMatrix * normal );

		if( useNormal == 1. ) {
			vTangent = normalize( normalMatrix * tangent.xyz );
			vBinormal = normalize( cross( vNormal, vTangent ) * tangent.w );
		} else {
			vTangent = vec3( 0. );
			vBinormal = vec3( 0. );
		}

		if( useRim > 0. ) {
			vEye = ( modelViewMatrix * vec4( position, 1.0 ) ).xyz;
		} else {
			vEye = vec3( 0. );
		}

		gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
	}
	</script>


	<script type="x-shader/x-fragment" id="semFrag">

	uniform sampler2D tNormal;
	uniform sampler2D tex;
	uniform float noise;
	uniform float useNormal;
	uniform float useRim;
	uniform float rimPower;
	uniform float normalScale;
	uniform float normalRepeat;

	varying vec2 vUv;
	varying vec3 vTangent;
	varying vec3 vBinormal;
	varying vec3 vNormal;
	varying vec3 vEye;
	varying vec3 vU;
	varying vec2 vN;

	void main() {
		
		vec3 finalNormal = vNormal;
		vec2 calculatedNormal = vN;

		if( useNormal == 1. ) {
			vec3 normalTex = texture2D( tNormal, vUv * normalRepeat ).xyz * 2.0 - 1.0;
			normalTex.xy *= normalScale;
			normalTex.y *= -1.;
			normalTex = normalize( normalTex );
			mat3 tsb = mat3( normalize( vTangent ), normalize( vBinormal ), normalize( vNormal ) );
			finalNormal = tsb * normalTex;

			vec3 r = reflect( vU, normalize( finalNormal ) );
			float m = 2.0 * sqrt( r.x * r.x + r.y * r.y + ( r.z + 1.0 ) * ( r.z+1.0 ) );
			calculatedNormal = vec2( r.x / m + 0.5,  r.y / m + 0.5 );
		}

		vec3 base = texture2D( tex, calculatedNormal ).rgb;
		
		// rim lighting

		if( useRim > 0. ) {
			float f = rimPower * abs( dot( vNormal, normalize( vEye ) ) );
			f = useRim * ( 1. - smoothstep( 0.0, 1., f ) );
	        base += vec3( f );
	    }

		gl_FragColor = vec4( base, 1. );

	}

	</script>

	
	<script src="scripts/three.js"></script>
	<script src="scripts/TessellateModifier.js"></script>

	<script src="scripts/jquery-1.11.2.js"></script>
	<script src="scripts/divaGlow.js"></script>
</body>
</html>